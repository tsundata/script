program : "program" variable ";" package? block "."

package : "import" variable ";" ( "import" variable ";" )*

block : declarations compound_statement

declarations : ( "var" ( variable_declaration ";" )+ )* function_declaration*

variable_declaration : ID ( "," ID )* type_spec

function_declaration : "func" ID ( "(" formal_parameter_list ")" )? ( ":" type_spec )? ";" block ";"

formal_params_list : formal_parameters
				   | formal_parameters ";" formal_parameter_list

formal_parameters : ID ( "," ID )* ":" type_spec

type_spec : INTEGER | FLOAT | STRING | BOOLEAN | LIST | DICT | MESSAGE

INTEGER : "int"
FLOAT   : "float"
STRING  : "string"
BOOLEAN : "bool"
LIST    : "list"
DICT    : "dict"
MESSAGE : "message"

compound_statement : "begin" statement_list "end"

statement_list : statement
			   | statement ";" statement_list

statement : compound_statement
		  | function_call_statement
		  | assignment_statement
		  | print_statement
		  | return_statement
		  | if_statement
		  | while_statement
		  | empty

return_statement : "return" expression?

print_statement : "print" expression

while_statement : "while" expression "do" statement_list "end"

if_statement : "if" expression "then" statement_list ( "else" statement_list )? "end"

expression : logic_or

logic_or : logic_and ( "or" logic_and )*

logic_and : equality ( "and" equality )*

equality : comparison ( ( "!=" | "==" ) comparison )*

comparison : expr ( ( ">" | ">=" | "<" | "<=" ) expr )*

function_call_statement : ( ID "." )? ID "(" ( expr ( "," expr )* )? ")"

function_reference : "@" ( ID "." )? ID

assignment_statement : variable ":=" expr

empty :

expr : term ( ( "+" | "-" ) term )*

term : factor ( ( "*" | "DIV" | "/" ) factor )*

factor : "+" factor
       | "-" factor
       | INTEGER_CONST
       | FLOAT_CONST
       | STRING_CONST
       | MESSAGE_CONST
       | LIST
       | DICT
       | BOOLEAN_CONST
       | "(" expr ")"
       | variable
       | function_call_statement
       | function_reference

variable : ID

MESSAGE_CONST : "#" DIGIT+
BOOLEAN_CONST : "true" | "false"
NUMBER        : DIGIT+ ( "." DIGIT+ )?
STRING_CONST  : "\"" <any char except "\"">* "\""
ID            : ALPHA ( ALPHA | DIGIT )*
ALPHA         : "a" ... "z" | "A" ... "Z" | "_"
DIGIT         : "0" ... "9"
LIST          : "[" factor ( "," factor )* "]"
DICT          : "{" STRING ":" factor ( "," STRING ":" factor )* "}"
